defmodule Mix.Tasks.Gettext.Translate do
  @moduledoc """
  Run Libretranslate locally loading only the locales we support.

  docker run -ti --rm -p 9999:5000 libretranslate/libretranslate --load-only en,es
  """

  @shortdoc "Translate all of the `.po` files using the `.pot` file(s)"

  use Mix.Task

  import Dotcom.Locales, only: [default_locale_code: 0, locale_codes: 0]

  @directory "priv/gettext"
  @url "http://localhost:9999/translate"

  @impl Mix.Task
  # Translate the `.pot` file into the given locales.
  # E.g., `mix gettext.translate --locales es,fr`
  def run(["--locales", locales]) do
    Finch.start_link(name: TranslateFinch)

    locales
    |> String.split(",")
    |> Enum.reject(fn locale ->
      locale === default_locale_code() || not Enum.member?(locale_codes(), locale)
    end)
    |> translate()
  end

  # Translate all non-default locales.
  def run([]) do
    Finch.start_link(name: TranslateFinch)

    (locale_codes() -- [default_locale_code()]) |> translate()
  end

  # Translate the given locales.
  defp translate(locales) when is_list(locales) do
    Enum.each(locales, &translate/1)
  end

  # Translate a single locale.
  defp translate(locale) when is_binary(locale) do
    Enum.each(domains(), fn domain ->
      write_domain_translations(domain, locale)
    end)
  end

  defp build_request(text, locale) do
    %{
      q: text,
      source: default_locale_code(),
      target: locale,
      alternatives: 3
    }
  end

  defp domain_line(line) do
    Regex.scan(~r/(?<=\")(.*?)(?=\")/, line)
    |> List.flatten()
    |> List.first()
  end

  defp domain_lines(domain) do
    "#{@directory}/#{domain}.pot"
    |> File.read!()
    |> String.split("\n", trim: true)
    |> Enum.filter(fn line -> String.match?(line, ~r/^msgid \"(.)+\"/) end)
    |> Enum.map(&domain_line/1)
  end

  defp domains() do
    @directory
    |> File.ls!()
    |> Enum.filter(fn ref -> String.match?(ref, ~r/.pot$/) end)
    |> Enum.map(fn ref -> String.split(ref, ".") |> List.first() end)
  end

  defp translate_domain(domain, locale) do
    domain_lines(domain)
    |> Enum.map(fn text -> {text, translate_text(text, locale)} end)
    |> Map.new()
  end

  defp translate_text(text, locale) do
    @url
    |> Req.post!(finch: TranslateFinch, json: build_request(text, locale))
    |> Map.get(:body)
    |> Map.new(fn {k, v} -> {Recase.to_snake(k), v} end)
  end

  defp write_domain_translations(domain, locale) do
    file = File.open!("#{@directory}/#{locale}/LC_MESSAGES/#{domain}.po", [:write, :utf8])

    IO.puts(file, "# THIS FILE WAS GENERATED BY RUNNING `mix gettext.translate`")
    IO.puts(file, "")

    translate_domain(domain, locale)
    |> Enum.each(fn {k, v} -> write_translation(file, k, v) end)
  end

  defp write_translation(file, text, translation) do
    IO.puts(file, "msgid \"#{text}\"")
    IO.puts(file, "msgstr \"#{Map.get(translation, "translated_text")}\"")

    Map.get(translation, "alternatives")
    |> Enum.each(fn alternative ->
      IO.puts(file, "# Alternative: #{alternative}")
    end)

    IO.puts("")
  end
end
